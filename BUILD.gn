# Copyright (c) 2019 The libsfz Authors
# This file is part of libsfz, a free software project.  You can redistribute it and/or modify it
# under the terms of the MIT License.

config("libsfz_private") {
  include_dirs = [
    "include/all",
    "src/all",
  ]
  if (target_os == "win") {
    include_dirs += [
      "include/win",
      "src/win",
    ]
  } else {
    include_dirs += [
      "include/posix",
      "src/posix",
    ]
  }
  if (current_toolchain != "//build/lib/win:msvc") {
    cflags = [
      "-Wall",
      "-Wextra",
      "-Wsign-compare",
      "-Wnarrowing",
      "-Werror",
    ]
  }
}

config("libsfz_public") {
  include_dirs = [ "include/all" ]
  if (target_os == "win") {
    include_dirs += [ "include/win" ]
  } else {
    include_dirs += [ "include/posix" ]
  }
}

static_library("libsfz") {
  sources = [
    "include/all/sfz/args.hpp",
    "include/all/sfz/digest.hpp",
    "include/all/sfz/encoding.hpp",
    "include/all/sfz/os.hpp",
    "src/all/sfz/args.cpp",
    "src/all/sfz/digest.cpp",
    "src/all/sfz/encoding.cpp",
    "src/all/sfz/format.cpp",
    "src/all/sfz/string-utils.cpp",
  ]
  if (target_os == "win") {
    sources += [
      "include/win/sfz/file.hpp",
      "src/win/sfz/error.cpp",
      "src/win/sfz/error.hpp",
      "src/win/sfz/file.cpp",
      "src/win/sfz/os.cpp",
      "src/win/sfz/win.hpp",
    ]
  } else {
    sources += [
      "include/posix/sfz/file.hpp",
      "src/posix/sfz/error.cpp",
      "src/posix/sfz/error.hpp",
      "src/posix/sfz/file.cpp",
      "src/posix/sfz/os.cpp",
    ]
  }
  public_deps = [ "//ext/procyon:procyon-cpp" ]
  configs += [ ":libsfz_private" ]
  public_configs = [ ":libsfz_public" ]
}

executable("args-test") {
  sources = [ "src/all/sfz/args.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("digest-test") {
  sources = [ "src/all/sfz/digest.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("encoding-test") {
  sources = [ "src/all/sfz/encoding.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("format-test") {
  sources = [ "src/all/sfz/format.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("optional-test") {
  sources = [ "src/all/sfz/optional.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("os-test") {
  sources = [ "src/all/sfz/os.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}

executable("string-utils-test") {
  sources = [ "src/all/sfz/string-utils.test.cpp" ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":libsfz",
    "//ext/gmock:gmock_main",
  ]
}
